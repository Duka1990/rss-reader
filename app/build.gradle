buildscript {
    dependencies {
        // android-apt
        // The android-apt plugin assists in working with annotation processors in combination with Android Studio.
        // see https://bitbucket.org/hvisser/android-apt
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

        classpath 'me.tatarka:gradle-retrolambda:3.7.0'

        // This dependency is needed to avoid lint errors because of using Retrolambda with Lombok.
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'realm-android'

android {
    compileSdkVersion 26
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26

        applicationId "hu.possible.demo.rssreader"
        versionCode 1
        versionName "1.0"
    }

    signingConfigs {
        demokey {
            storeFile file("../signing-keys/demokey.jks")
            storePassword "defaultKeyDemo"
            keyAlias "RSSReaderDemo"
            keyPassword "defaultKeyDemo"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            signingConfig signingConfigs.demokey
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        develop {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.demokey
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name.equals('debug')) {
            variant.setIgnore(true);
        }

        null;
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/rxjava.properties'
    }

    android.applicationVariants.all { variant ->
        // NOTE
        // The doc a http://tools.android.com/tech-docs/new-build-system/user-guide says each variant has a read/write outputFile property, but
        // during compilation an error occurs complaining about no such property. Thus we need to use the outputs collection.

        variant.outputs.each { output ->

            def apkName = "${variant.mergedFlavor.applicationId}-v${variant.mergedFlavor.versionName}_${variant.mergedFlavor.versionCode}-${variant.buildType.name}.apk";

            output.outputFile = new File(output.outputFile.parent, apkName)
        }
    }
}

dependencies {
    // For generating getters, setters, toStrings thus removing boilerplate code
    // see http://projectlombok.org/setup/android.html
    // Gradle 2.0 might have problems, see http://stackoverflow.com/questions/22891606/provided-scope-not-working-android-studio-with-gradle
    provided 'org.projectlombok:lombok:1.16.18'
    annotationProcessor 'org.projectlombok:lombok:1.16.18'

    // Support libraries
    compile 'com.android.support:appcompat-v7:26.0.2'
    compile 'com.android.support:cardview-v7:26.0.2'
    compile 'com.android.support:support-v4:26.0.2'
    compile 'com.android.support:percent:26.0.2'
    compile 'com.android.support:design:26.0.2'

    // Image loading library
    compile 'com.github.bumptech.glide:glide:4.0.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0'

    // REST client dependencies
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile ('com.squareup.retrofit2:converter-simplexml:2.3.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'

    // RxJava integration
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Dependency injection with Dagger
    compile 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    provided 'javax.annotation:jsr250-api:1.0'

    // View injection
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Logging
    compile 'com.jakewharton.timber:timber:4.5.1'
}
